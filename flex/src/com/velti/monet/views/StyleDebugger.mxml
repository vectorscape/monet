<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	height="150" width="215" title="Style Debugger"
	visible="{URLUtil.urlParams.styleDebug == 'true'}"
	creationComplete="panel1_creationCompleteHandler(event)">
	
	<mx:Script>
	<![CDATA[
		import com.velti.monet.utils.URLUtil;
		
		import mx.core.Application;
		import mx.core.UIComponent;
		import mx.events.FlexEvent;
		import mx.managers.PopUpManager;
		
		[Bindable]
		public var text:String = "Hold alt and mouse over a component.";
		
		private function onMouseMove(event:MouseEvent):void
		{
			var styleText:String = "";
			if(event.target is UIComponent && event.altKey) {
				var comp:UIComponent = event.target as UIComponent;
				styleText += comp.id;
				styleText += "\nstyleName: "+comp.styleName;
				styleText += "\nfontFamily: "+comp.getStyle("fontFamily");
				styleText += "\nfontSize: "+comp.getStyle("fontSize");
				styleText += "\nfontWeight: "+comp.getStyle("fontWeight");
				var color:uint = comp.getStyle("color") as uint;
				styleText += "\ncolor: #" + color.toString(16);
				text = styleText;
			}
		}
		
		protected function panel1_creationCompleteHandler(event:FlexEvent):void
		{
			Application.application.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			if(this.parent)
				this.parent.removeChild(this);
			PopUpManager.addPopUp(this,Application.application as DisplayObject);
			PopUpManager.centerPopUp(this);
		}
		
	]]>
	</mx:Script>
	<mx:TextArea id="textArea" width="100%" height="100%"
		text="{text}" />
</mx:TitleWindow>
